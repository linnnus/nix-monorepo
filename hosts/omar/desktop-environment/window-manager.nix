{pkgs, ...}: {
  services.xserver.windowManager.dwm.enable = true;

  # Show battery and clock in status bar. This is a background daemon which
  # updates the root window, which DWM uses for status.
  systemd.user.services.dwm-battery = {
    description = "Battery status update";
    partOf = ["graphical-session.target"];
    wantedBy = ["graphical-session.target"];

    serviceConfig.ExecStart = pkgs.writeShellScript "dwm-battery" ''
      while true; do
        echo -n "$(date +%H:%M) - ";

        # See: https://www.kernel.org/doc/Documentation/ABI/testing/sysfs-class-power
        case "$(cat /sys/class/power_supply/BAT0/status)" in
          Charging)       echo -n "ðŸ”‹ " ;;
          Discharging)    echo -n "ðŸª« " ;;
          "Not charging") echo -n "ðŸª« " ;;
          Full)           echo -n "ðŸ”‹ " ;;
          Unknown)        echo -n "? " ;;
          ""|*)           echo -n "?? " ;;
        esac

        echo -n "$(cat /sys/class/power_supply/BAT0/capacity)%"

        echo
        sleep 5
      done | ${pkgs.dwm-setstatus}/bin/dwm-setstatus
    '';
  };

  environment.systemPackages = with pkgs; [
    st
    dmenu
  ];
}
